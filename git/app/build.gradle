import java.util.regex.Pattern
import com.slin.version.plugin.*
plugins {
    id 'com.slin.version.plugin'
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}


android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.slin.git"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        GitProperties properties = new GitProperties(rootProject, "public.properties", "local.properties")
        buildConfigField "String", "CLIENT_ID", properties.getValue("CLIENT_ID")
        buildConfigField "String", "CLIENT_SECRET", properties.getValue("CLIENT_SECRET")
        buildConfigField "String", "USER_ACCESS_TOKEN", properties.getValue("USER_ACCESS_TOKEN")
        buildConfigField "String", "DEFAULT_ACCOUNT", properties.getValue("DEFAULT_ACCOUNT")
        buildConfigField "String", "DEFAULT_PASSWORD", properties.getValue("DEFAULT_PASSWORD")

    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        dataBinding = true
        //如果使用dataBinding的话，只设置dataBinding的话不会自动生成对应的binding文件，需要编译才行，因此添加此设置
        viewBinding = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(path: ':git:core')
    implementation project(path: ':git:proto')

//    implementation 'com.github.dxslin.StudyKotlin:core:2.0.0'

    implementation Dependencies.kotlin_stdlib
    implementation Dependencies.appcompat
    implementation Dependencies.swipe_refresh_layout

    implementation Dependencies.material
    implementation Dependencies.constraintlayout
    implementation Dependencies.vector_drawable
    implementation Dependencies.navigation_fragment
    implementation Dependencies.navigation_ui
    implementation Dependencies.navigation_fragment_ktx
    implementation Dependencies.navigation_ui_ktx
    implementation Dependencies.dynamic_animation
    implementation Dependencies.lifecycle_extensions
    implementation Dependencies.lifecycle_runtime
    //room
    implementation Dependencies.room
    implementation Dependencies.room_ktx
    kapt Dependencies.room_compiler
    //paging
    implementation Dependencies.paging_runtime

    implementation Dependencies.hilt
    kapt Dependencies.hilt_compiler
    implementation Dependencies.hilt_viewmodel
    kapt Dependencies.hilt_viewmodel_compiler

//    test
    testImplementation Dependencies.junit
    androidTestImplementation Dependencies.junit_ext
    androidTestImplementation Dependencies.espresso_core


}

kapt {
    arguments {
        arg("room.schemaLocation", "$projectDir/schemas".toString())
    }
}


class GitProperties {

    private static def STRING_PATTERN = Pattern.compile("^\"([\\w-]*)\"")

    private Properties publicProperties
    private Properties localProperties

    GitProperties(Project project, String publicFile, String localFile) {
        publicProperties = new Properties()
        publicProperties.load(project.file(publicFile).newDataInputStream())
        localProperties = new Properties()
        localProperties.load(project.file(localFile).newDataInputStream())
    }

    String getValue(String key) {
        String publicValue = getStringValue(publicProperties, key)
        String localValue = getStringValue(localProperties, key)
        def result = or(publicValue, localValue)
        return "\"" + result + "\""
    }

    static String or(String self, String another) {
        return self == null || self.isEmpty() ? another : self
    }

    static String getStringValue(Properties properties, String key) {
        String value = properties.getProperty(key)
        if(value == null) value = "\"\""
        def matcher = STRING_PATTERN.matcher(value)
        if (!matcher.matches()) {
            throw IllegalFormatException("请输入字符串，比如：\"a123\"")
        } else {
            return matcher.group(1)
        }
    }
}